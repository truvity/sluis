# Development commands for sluis. Tools come from devbox
# (`devbox shell`, or direnv); CI runs each recipe as its own job.

# Disable go.work (a parent workspace interferes with standalone module builds)
export GOWORK := "off"

charts := "sluis"

# The tools `telemetry` fetches, pinned: dashboardlint is truvity/observability's
# own lint of its dashboard contract, vmalert-tool is VictoriaMetrics' rule
# unit-tester, the engine of the estate's own ruler.
observability_version := "v0.43.3"
vmutils_version := "v1.152.0"

# Format all Go files
fmt:
    golangci-lint fmt ./...

# Build (compile check)
build: fmt console cross
    go build ./...

# Compile sluisctl for every platform the release builds it for.
#
# `go build ./...` proves the host platform and nothing else, so a call
# that does not exist elsewhere — syscall.Flock, which Windows has no
# equivalent name for — compiles here, passes CI, merges, and is first
# refused by goreleaser, with the change already on master and a version
# already burned on the tag (2026-09-22, v1.25.1).
#
# Only sluisctl: it is the one binary .goreleaser.yaml builds for
# Windows, on the grounds that a laptop is a laptop. The rest are Linux
# and macOS, which `go build ./...` plus CI's own runner already cover.
cross:
    #!/usr/bin/env bash
    set -euo pipefail
    for target in windows/amd64 windows/arm64 darwin/amd64 darwin/arm64; do
        GOOS="${target%%/*}" GOARCH="${target##*/}" go build -o /dev/null ./cmd/sluisctl
    done

# Run unit tests
# `console` first, and the same on every recipe that COMPILES Go: CI
# runs each recipe as its own job in a fresh checkout, so nothing else
# has built the bundle the binary embeds. Locally this is invisible --
# `check` runs `build` first and the bundle is already there -- and in CI
# it is `pattern all:dist: no matching files found`, four jobs at once.
test: console
    go test ./... -coverprofile=coverage.out

# The LocalStack the S3 Blob and DynamoDB State adapters are tested against. Pinned
# by digest, and the community 4.x line: LocalStack's `latest` and `stable` now
# resolve to a licensed build that exits without a token, which would fail every
# fork's CI with a message its author cannot fix. A moving tag also changes the
# test. Keep it equal to the image in .github/workflows/ci.yaml.
s3_image := "localstack/localstack@sha256:3ebc37595918b8accb852f8048fef2aff047d465167edd655528065b07bc364a"

# The S3 Blob and DynamoDB State adapters against LocalStack, started
# with `docker run` (no testcontainers) and removed afterwards. Not part of
# `check`, which needs nothing but the checkout; CI runs it as its own job. It
# fails if the conformance tests skipped (hack/s3-conformance.sh,
# hack/dynamodb-conformance.sh).
test-s3:
    #!/usr/bin/env bash
    set -euo pipefail
    # A name of its own: other checkouts run LocalStack too.
    docker rm -f sluis-s3-dynamodb >/dev/null 2>&1 || true
    docker run -d --name sluis-s3-dynamodb -p 4566:4566 -e SERVICES=s3,kms,sqs,dynamodb {{s3_image}} >/dev/null
    trap 'docker rm -f sluis-s3-dynamodb >/dev/null 2>&1 || true' EXIT
    for i in $(seq 1 40); do
        curl -sf -m 3 http://localhost:4566/_localstack/health >/dev/null 2>&1 && break
        sleep 3
    done
    ACCESS_ROSTER_S3_URL=http://localhost:4566 hack/s3-conformance.sh
    ACCESS_ROSTER_DYNAMODB_URL=http://localhost:4566 hack/dynamodb-conformance.sh

# Run linters. `config verify` first: `run` accepts unknown top-level keys
# silently, so a settings block in the wrong place is otherwise invisible.
lint: console
    golangci-lint config verify
    golangci-lint run ./...
    cd deploy/pulumi && GOWORK=off golangci-lint run ./...
    # A `;` inside a mermaid sequenceDiagram is a STATEMENT SEPARATOR, not
    # punctuation: it splits the message text in half, the second half
    # parses as a statement with no arrow, and GitHub renders "Unable to
    # render rich display" in place of the whole diagram. Nothing in the
    # normal build reads these files, so the first reader to notice is
    # somebody looking at the documentation.
    ! grep -rn --include=*.md -E '^[[:space:]]*[A-Za-z][A-Za-z0-9_]*[[:space:]]*-?->>?.*;' docs/

# The Pulumi library (deploy/pulumi) is a module of its own so that Pulumi is
# not in the root's dependency graph; the root has no go.work, and `./...` does
# not descend into a nested module, so the root build, test and lint never see
# it and this recipe is its only gate. Its tests use Pulumi's mocks, so they
# create nothing and need no credentials, and the rendered `ports:` block is
# validated against the schemas in schemas/config, so the library cannot drift
# from the binaries it configures.
pulumi-test:
    cd deploy/pulumi && GOWORK=off go vet ./... && GOWORK=off go test -count=1 ./...

# Run Go vulnerability check. Deliberately not part of `check`: a newly
# published CVE in a dependency must not turn a PR that never touched it
# red. `.github/workflows/security.yaml` runs this as its own job.
vuln: console
    govulncheck ./...

# Regenerate gen/ from proto/ (buf + protoc-gen-go + protoc-gen-connect-go,
# all from devbox). Generated code is COMMITTED so the module is
# `go get`-able without buf installed.
generate:
    buf lint
    buf generate

# Acceptance against a real API server, in a throwaway kind cluster.
#
# Everything else runs against fakes, and the fakes are silent about the
# three things this checks: a real API server validates object names,
# refuses a create that raced another, and is the only thing that can
# answer a TokenReview — which is what recovery and the API listener's
# guard are built on.
acceptance: console
    kind create cluster --name sluis-acceptance
    kubectl --context kind-sluis-acceptance create namespace acceptance
    go run ./cmd/acceptance -namespace acceptance -kubeconfig ""
    kind delete cluster --name sluis-acceptance

# Check the release configuration without cutting one, and, given the tag
# about to be cut (`just release-check v1.64.0`), refuse a tag the Pulumi
# library does not agree with.
#
# The require gate: deploy/pulumi requires github.com/truvity/sluis, and a
# library tagged vX.Y.Z whose require names another version ships against
# the wrong root. hack/check-release-require.sh passes when the require
# names the tag or is gone. The release workflow runs the same script first.
#
# The release path, as far as it can be exercised without a tag.
#
# `goreleaser check` validates the config and `build --single-target`
# proves it compiles, but NEITHER reaches the archives stage — which is
# where v0.12.0 failed, four minutes into a tagged run, publishing
# nothing. So the archive shapes are checked here by reading the same
# file goreleaser reads.
release-check tag="": console
    #!/usr/bin/env bash
    set -euo pipefail
    if [ -n "{{tag}}" ]; then ./hack/check-release-require.sh "{{tag}}"; fi
    ./hack/check-archives.py
    goreleaser check
    goreleaser build --snapshot --clean --single-target

# The cheap half of release-check, for `check`: it reads a file and
# needs no compiler, so it costs nothing to run on every push.
archive-check:
    ./hack/check-archives.py

# The reason this repository can be public. Runs in CI as its own job.
leak-canary:
    hack/leak-canary.sh

# Every Go symbol the documentation names must exist. Nothing compiles a
# code block in a Markdown file, so a rename leaves the old name in the
# guide and the first person to notice is a stranger following it. Every
# proto service and RPC must also be named in docs/reference/contracts.md.
#
# And the documentation's hygiene (hack/check-docs-hygiene.py): every relative
# link in a Markdown file or Chart.yaml resolves, and the retired names
# (access-roster, access-issuer, NATS) appear only where
# hack/docs-hygiene-allow.tsv says, with a reason.
docs-check:
    ./hack/check-docs-symbols.py
    ./hack/check-docs-hygiene.py
    go test -count=1 ./internal/contractsdoc/ ./internal/port/matrixdoc/

# Regenerate docs/reference/adapters.md from the adapter registry. Run it
# after adding, removing or changing an adapter; docs-check fails when stale.
adapters-doc:
    go run ./internal/port/matrixdoc/gen

# Run go mod tidy
tidy:
    go mod tidy

# Clean build artifacts
clean:
    rm -rf dist/ frontend/dist/ ts/dist/ coverage.out

# Lint the chart, compare its golden renders, and render every
# negative fixture.
#
# The schema is part of the lint: an unknown key must fail the render,
# not be silently ignored. Everything a schema cannot express -- a value
# the service cannot start without, a posture that admits nobody, one
# route described twice -- is refused by the chart's own render-time
# validation. Every rule of either kind has a fixture under
# tests/invalid/<chart>/ that must FAIL, and fail for the reason on its
# second line (`# error: ...`): a fixture that fails for some other
# reason proves nothing about its own rule.
#
# The golden renders (tests/cases -> tests/golden, `just golden` to
# regenerate) pin every byte of output. The checks after them are the
# properties a regenerated golden could lose without anyone noticing in
# review.
chart-lint:
    #!/usr/bin/env bash
    set -euo pipefail
    for chart in {{ charts }}; do
      # Bare first: the shipped values must satisfy their own schema, or
      # anyone who lints the chart as published gets a failure.
      helm lint "charts/$chart"
      helm lint "charts/$chart" -f "tests/cases/$chart/minimal/values.yaml"
      # `if`, not `!`: under `set -e` a negated command that fails does
      # not stop the script, so `! cmd` would check nothing.
      if helm template sluis "charts/$chart" --set bogusKey=1 >/dev/null 2>&1; then
        echo "$chart: an unknown key rendered" >&2
        exit 1
      fi
      for values in tests/invalid/"$chart"/*.yaml; do
        if err="$(helm template sluis "charts/$chart" -f "$values" 2>&1 >/dev/null)"; then
          echo "RENDERED BUT SHOULD HAVE FAILED: $values" >&2
          exit 1
        fi
        want="$(sed -n '2s/^# error: //p' "$values")"
        if [ -z "$want" ]; then
          echo "NO '# error:' LINE: $values" >&2
          exit 1
        fi
        if ! grep -qF -- "$want" <<<"$err"; then
          printf 'FAILED FOR ANOTHER REASON: %s\n  want: %s\n  got:  %s\n' "$values" "$want" "$err" >&2
          exit 1
        fi
      done
      # Every example the chart SHIPS must render on top of its own
      # minimal values. An example is something a reader copies, so one
      # that no longer renders is a broken instruction found by a
      # stranger; nothing else in this repository reads these files.
      for example in charts/"$chart"/examples/*.yaml; do
        [ -e "$example" ] || continue
        helm template sluis "charts/$chart" \
          -f "tests/cases/$chart/minimal/values.yaml" -f "$example" >/dev/null
      done
      echo "$chart: schema and $(ls tests/invalid/"$chart"/*.yaml | wc -l | tr -d ' ') negative fixtures OK"
    done
    hack/golden.sh
    # The schemas are generated, and the binaries and the chart are held to
    # the committed files: a diff here is a builder changed without
    # `just config-schemas`.
    just config-schemas
    git diff --exit-code -- schemas/config charts/sluis/values.schema.json
    # What the chart renders for each component's `config` is what the values
    # say, and is a file that component's binary accepts. Required rather than
    # skipped: a test that quietly does not run proves nothing.
    ACCESS_ROSTER_REQUIRE_HELM=1 go test -count=1 ./tests/chart/
    # `push` is a chart-side instruction to External Secrets and not part
    # of an App's declaration: the service's loader refuses a key it does
    # not know, so an entry's push block reaching the rendered catalogue
    # would stop the service at start.
    if grep -n '^      push:' tests/golden/sluis/*.yaml; then
      echo "a catalogue entry's push block reached the rendered catalogue" >&2
      exit 1
    fi
    # "/console" and "/console/" are the same place: both spellings must
    # render the same, and never a route to "/console//".
    diff tests/golden/sluis/route.yaml tests/golden/sluis/route-trailing-slash.yaml
    if grep -l 'console//' tests/golden/sluis/*.yaml; then exit 1; fi
    # Every backendRefs entry writes `weight` out. A desired/live
    # comparison normalises core-API defaults but not CRDs, so a field the
    # API server fills in is a permanent diff. Routes and policies alike.
    for golden in tests/golden/*/*.yaml; do
      test "$(yq ea '[.. | select(tag == "!!map" and has("backendRefs")) | .backendRefs[] | select(has("weight") | not)] | length' "$golden")" = "0"
    done
    # Every PushSecret dataTo entry carries its own storeRef. external-
    # secrets validates this with a CEL rule in the CRD, so nothing that
    # only RENDERS can see it: helm template, the values schema and the
    # golden diff all pass on a manifest the API server refuses outright
    # with "storeRef must specify either name or labelSelector". It
    # shipped that way once, and Argo retried the rejected task forever
    # while the resource simply never existed.
    for golden in tests/golden/*/*.yaml; do
      test "$(yq ea '[.. | select(tag == "!!map" and has("dataTo")) | .dataTo[] | select(has("storeRef") | not)] | length' "$golden")" = "0"
    done

# The chart's alert rules and dashboard, held to what the estate holds them to.
#
# The dashboard is generated (hack/dashboards/access-roster-overview.py) and the
# committed JSON is held to the generator. `dashboardlint`, from
# truvity/observability at the version pinned above, judges it against that
# repository's dashboard contract (docs/dashboards.md): a datasource variable
# every panel uses, a cluster variable, `$cluster` in the title and every
# query. The refusal is proved to be real, too: the same dashboard with a
# literal datasource must fail.
#
# The rules are unit-tested with vmalert-tool (a pinned, checksum-verified
# release) through tests/chart: every rule has a test that fires it and one
# that must not, and the test refuses a rule without both.
telemetry:
    #!/usr/bin/env bash
    set -euo pipefail
    python3 hack/dashboards/access-roster-overview.py | diff - charts/sluis/dashboards/access-roster-overview.json

    tools=$(mktemp -d); trap 'rm -rf "$tools"' EXIT
    GOBIN="$tools" go install github.com/truvity/observability/cmd/dashboardlint@{{observability_version}}
    "$tools/dashboardlint" charts/sluis/dashboards/*.json
    # The refusal is real: the same dashboard with a literal datasource must fail.
    sed 's/"uid": "${datasource}"/"uid": "a-literal-uid"/' charts/sluis/dashboards/access-roster-overview.json > "$tools/pinned.json"
    if "$tools/dashboardlint" "$tools/pinned.json" 2>"$tools/pinned.err"; then
        echo "dashboardlint accepted a dashboard pinned to one datasource" >&2; exit 1
    fi
    grep -q "literal datasource" "$tools/pinned.err"

    os=$(uname -s | tr '[:upper:]' '[:lower:]')
    case "$(uname -m)" in x86_64) arch=amd64 ;; aarch64|arm64) arch=arm64 ;; *) echo "no vmutils for $(uname -m)" >&2; exit 1 ;; esac
    tarball="vmutils-$os-$arch-{{vmutils_version}}.tar.gz"
    base="https://github.com/VictoriaMetrics/VictoriaMetrics/releases/download/{{vmutils_version}}"
    cache="${XDG_CACHE_HOME:-$HOME/.cache}/sluis/vmutils-{{vmutils_version}}-$os-$arch"
    if [ ! -x "$cache/vmalert-tool-prod" ]; then
        mkdir -p "$cache"
        curl -fsSL "$base/$tarball" -o "$tools/$tarball"
        curl -fsSL "$base/${tarball%.tar.gz}_checksums.txt" -o "$tools/sums.txt"
        (cd "$tools" && grep " $tarball\$" sums.txt | sha256sum -c -)
        tar -xzf "$tools/$tarball" -C "$cache" vmalert-tool-prod
    fi
    ACCESS_ROSTER_REQUIRE_HELM=1 ACCESS_ROSTER_REQUIRE_VMALERT=1 ACCESS_ROSTER_VMALERT_TOOL="$cache/vmalert-tool-prod" go test -count=1 ./tests/chart/

# The JSON Schema of each binary's configuration file, written into
# schemas/config/ from internal/config/schema, and the chart's values schema,
# which embeds them under each component's `config`. The binaries embed the
# committed files, so a change to the builder that is not followed by this
# fails `go test` and the drift check in chart-lint.
config-schemas:
    go run ./internal/config/gen

# Regenerate the golden renders -- review the diff before committing.
golden:
    hack/golden.sh update

# Install every toolchain dependency, on both sides. Separate from the
# builds because `npm ci` is the slow part and it does not change
# between them.
#
# `tidy` first: downloading what go.mod asks for is not much use if
# go.mod is missing something the code imports, and the two together are
# what "my dependencies are in order" means.
#
# This does NOT touch gen/. Code generated from proto/ stays committed:
# it is Go source, small and diffable, and it is what makes the module
# `go get`-able without buf installed. That is a different argument from
# a minified bundle, which is neither small nor diffable.
deps: tidy
    go mod download
    cd ts && npm ci
    cd frontend && npm ci

# Build the TypeScript package into ts/dist.
#
# NOT committed, and FIRST: the console's package.json depends on it as
# `file:../ts`, and resolves through the root manifest's `main`, which
# points into ts/dist. Build the console before this and it resolves an
# import to a directory that is not there yet.
#
# The release builds it the same way and publishes the root package to
# GitHub Packages; nothing builds it on install.
ts-package: deps
    cd ts && npx tsc -p tsconfig.build.json

# Typecheck and test the TypeScript package.
ts: ts-package
    cd ts && npx tsc --noEmit && npx vitest run

# Build the console SPA into frontend/dist, which the Go binary embeds.
#
# NOT committed, and `build` depends on it so the embed always has
# something current to read. There is no drift check any more because
# drift is not possible: the bundle is produced from the lockfile every
# time, and if it is absent the compiler says so.
#
# The console's own unit tests run first: the pure models its pages read
# (which App needs you, what a page's first line says) and the router's
# moved addresses. They need no browser.
console: ts-package
    cd frontend && npm test
    cd frontend && npm run build

# The audit catalogue against the audit component's own toolchain, at the
# version go.mod pins: the document is valid, and every action the code
# emits is declared and every declared one emitted. A name built at run
# time is invisible to it, which is why every action is spelled once, in
# internal/audit/events.go.
audit-catalogue:
    #!/usr/bin/env bash
    set -euo pipefail
    version=$(go list -m -f '{{{{.Version}}' github.com/truvity/audit/sdk)
    audit="go run github.com/truvity/audit/cmd/audit@${version}"
    $audit validate internal/audit/catalogue/roster.yaml
    # The Go code only: the console carries every action name in its
    # generated sentences, which would pass for emitting them.
    $audit check-emitters internal --catalogue internal/audit/catalogue/roster.yaml
    # The console's copy of the sentences is generated from the same
    # document, and a diff here is a catalogue changed without it.
    just audit-sentences
    git diff --exit-code -- frontend/src/auditSentences.ts

# The catalogue's sentences for the console's Audit page, generated from
# internal/audit/catalogue/roster.yaml by the audit component's toolchain.
audit-sentences:
    #!/usr/bin/env bash
    set -euo pipefail
    version=$(go list -m -f '{{{{.Version}}' github.com/truvity/audit/sdk)
    {
        echo '// Code generated by `just audit-sentences` from internal/audit/catalogue/roster.yaml. DO NOT EDIT.'
        echo 'import type { Sentences } from "@truvity/audit";'
        echo
        printf 'export const roster: Sentences = '
        go run "github.com/truvity/audit/cmd/audit@${version}" messages internal/audit/catalogue/roster.yaml
    } > frontend/src/auditSentences.ts

# Run all checks (build + test + lint + chart-lint + telemetry + leak-canary)
# Everything CI runs, so that the pre-push hook catches what CI would.
# `vuln` is deliberately not here: run it on its own with `just vuln`,
# the same way `.github/workflows/security.yaml` does.
#
# `ts` is in here despite being slow: it typechecks and tests the
# published package, which nothing else does. `console` arrives through
# `build`, which needs it.
check: build test pulumi-test lint chart-lint telemetry archive-check docs-check leak-canary audit-catalogue ts
